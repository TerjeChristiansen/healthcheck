/*
 * Copyright 2022 Storebrand ASA
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// GradleWrapper version specifier.
wrapper {
    gradleVersion = "7.5.1"
    setDistributionType(Wrapper.DistributionType.ALL)
}

allprojects {
    group = 'no.storebrand.healthcheck'
    version = '0.3.2+2023-02-09'
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
        options.deprecation = true
        options.incremental = true
        options.compilerArgs += ['-Werror', '-Xlint:all', '-Xlint:-serial']
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        // without the -quiet option, the build fails due to errors&warns
        options.addStringOption('Xdoclint:none', '-quiet')
    }


    // Common versions
    ext {
        slf4jVersion = '1.7.+'
        junitVersion = '4.13.2'
        mockitoVersion = '3.12.0'
        springVersion = '4.3.30.RELEASE' // Still using Spring 4 as "baseline", but also fully compatible with Spring 5.

        // NOTE: Spring started supporting JUnit Jupiter with the release of Spring 5.0.0.
        springVersionForJupiter = '5.3.7'

        jacksonVersion = '2.12.4'

        javaxInjectVersion = '1'
    }

    project.buildDir = 'build-gradle'

    repositories { mavenCentral() }

    task allDependencies(type: DependencyReportTask) {}

    // ------------------------------------------------------------------------------------------
    // Local release configuration. The release file is not checked in to version control,
    // and should instead be kept locally to configure how to deploy this.
    if (file("${ rootProject.projectDir }/repositories.gradle").exists()) {
        apply from: "${ rootProject.projectDir }/repositories.gradle"
    }

    // ------------------------------------------------------------------------------------------
    // Maven Central publishing configuration, via Sonatype

    signing {
        sign publishing.publications
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = 'Storebrand HealthCheck'
                    description = 'Java library that makes it easy to report the current health of a service.'
                    url = 'https://github.com/storebrand/healthcheck'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'stolsvik'
                            name = 'Endre St√∏lsvik'
                            email = 'endre@stolsvik.com'
                        }
                        developer {
                            id = 'khiim'
                            name = 'Kristian Hiim'
                            email = 'kristian@hiim.no'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/storebrand/healthcheck.git'
                        developerConnection = 'scm:git:ssh://github.com:storebrand/healthcheck.git'
                        url = 'https://github.com/storebrand/healthcheck'
                    }
                }
                from components.java
            }
        }
        repositories {
            maven {
                name 'SonatypeOSSRHSnapshot'
                url 'https://oss.sonatype.org/content/repositories/snapshots'
                credentials {
                    // using findProperty(..) to be able to build without it being defined.
                    username findProperty('sonatypeUsername') as String ?: "should be in different file"
                    password findProperty('sonatypePassword') as String ?: "should be in different file"
                }
            }
            maven {
                name 'SonatypeOSSRHStaging'
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                credentials {
                    // using findProperty(..) to be able to build without it being defined.
                    username findProperty('sonatypeUsername') as String ?: "should be in different file"
                    password findProperty('sonatypePassword') as String ?: "should be in different file"
                }
            }
        }
    }
    // "Alias-tasks" to publish to Sonatype's OSSRH (OSS Repository Hosting), Snapshot and Staging
    task publishSnapshot(dependsOn: [publishAllPublicationsToSonatypeOSSRHSnapshotRepository])
    task publishStaging(dependsOn: [publishAllPublicationsToSonatypeOSSRHStagingRepository])
}
